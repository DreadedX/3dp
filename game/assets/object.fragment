#version 330 core

struct Material {
    sampler2D diffuse;
    sampler2D normal;
    sampler2D specular;
    float shininess;
};

struct Light {
    vec3 position;
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};

in vec3 FragPosition;
in vec3 Normal;
in vec2 Texcoord;

out vec4 color;

uniform Material material;
uniform Light light;
uniform vec3 viewPosition;

void main() {

    // Ambient
    vec3 ambient = light.ambient * texture(material.diffuse, Texcoord).rgb;

    // Normal
    vec3 norm = normalize(Normal);

    // Diffuse
    vec3 lightDir = normalize(light.position - FragPosition);
    float diff = max(dot(norm, lightDir), 0.0);
    vec3 diffuse =  light.diffuse * diff * texture(material.diffuse, Texcoord).rgb;

    // Specular
    vec3 viewDir = normalize(viewPosition - FragPosition);
    vec3 halfwayDir = normalize(lightDir + viewDir);
    vec3 reflectDir = reflect(-lightDir, norm);
    float spec = pow(max(dot(norm, halfwayDir), 0.0), material.shininess);
    vec3 specular = light.specular * spec * texture(material.specular, Texcoord).rgb;

    color = vec4(ambient + diffuse + specular, 1.0);
    //color = texture(material.specular, Texcoord);

    // Gamma correction
    float gamma = 2.2;
    color.rgb = pow(color.rgb, vec3(1.0/gamma));
}
